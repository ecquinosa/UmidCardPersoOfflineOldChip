Imports System.Data.OleDb
Imports System.Windows.Forms
Imports System.Drawing

''' <summary>
''' Data Access Class for OleDB Queries.
''' </summary>
''' <remarks>
''' Code By : BARON PATRICK T. PAREDES<br/>
''' Contact#: 0927-8497119<br/>
''' Email   : barspars@yahoo.com<br/>
''' Email   : baronpatrick.paredes@gmail.com<br/>
''' Date    : December 01, 2009
''' </remarks>
Public Class ClassDataAccessOleDB

    Implements IDisposable

#Region "Members"

    Private isDisposed As Boolean = False

    Private member_Connection As OleDbConnection
    Private member_Command As OleDbCommand
    Private member_DataReader As OleDbDataReader
    Private member_DataAdapter As OleDbDataAdapter

    Private member_DataTable As DataTable

    Private member_Exception As String
    Private member_ConnectionString As String

#End Region

#Region "Constuctor"

    ''' <summary>
    ''' Constructing New Data Access Object
    ''' </summary>
    ''' <param name="ConnectionString">A Valid OleDB Connection String.</param>
    ''' <remarks>Use connection string generated by a UDL File.</remarks>
    ''' <example>Dim Obj As New ClassDataAccess(ValidConnectionString)</example>
    Public Sub New(ByVal ConnectionString As String)
        member_ConnectionString = ConnectionString
    End Sub

#End Region

#Region "Properties"

    ''' <summary>
    ''' Exception Property of the object.
    ''' </summary>
    ''' <returns>Returns the error exception thrown by the object.</returns>
    Public ReadOnly Property Exception() As String
        Get
            Return member_Exception
        End Get
    End Property

    ''' <summary>
    ''' An OleDBbDataReader Object returned after executing a successful execution of a SQL Command.
    ''' </summary>
    ''' <returns>Current OleDbDataReader in object memory.</returns>
    Public ReadOnly Property DataReader() As OleDbDataReader
        Get
            Return member_DataReader
        End Get
    End Property

    ''' <summary>
    ''' A DataTable Object returned after executing a successful execution of a SQL Command.
    ''' </summary>
    ''' <returns>Current DataTable in object memory.</returns>
    Public ReadOnly Property DataTable() As DataTable
        Get
            Return member_DataTable
        End Get
    End Property

#End Region

#Region "Data Functions"

    ''' <summary>
    ''' Closes the connection properly.
    ''' </summary>
    Public Sub CloseConnection()
        If member_Connection.State = ConnectionState.Open Then member_Connection.Close()
    End Sub

    Public Function ConnectWithoutDisconnecting() As Boolean

        Try
            member_Connection = New OleDbConnection(member_ConnectionString)
            member_Connection.Open()
        Catch ex As Exception
            member_Exception = ex.ToString
            Return False
        End Try

        Return True

    End Function

    Public Function Execute(ByVal SQLQuery As String) As Boolean

        Try

            member_Command = New OleDbCommand

            member_Command.CommandText = SQLQuery
            member_Command.Connection = member_Connection

            member_Command.ExecuteNonQuery()
            member_Command.Dispose()

        Catch ex As Exception
            member_Exception = ex.ToString
            Return False
        End Try

        Return True

    End Function

    Public Enum ConnectionType As Integer
        DataReader
        DataCommand
        DataAdapter
    End Enum

    ''' <summary>
    ''' Connect using the connection string provided by the constructor.
    ''' </summary>
    ''' <param name="ConnectionString">A Valid OleDB Connection String.</param>
    ''' <returns>True=Connection Success, False=Connection Failed.</returns>
    ''' <example>Obj.Connect(ValidConnectionString)</example>
    Public Function Connect(ByVal ConnectionString As String) As Boolean

        Try

            member_Connection = New OleDbConnection(ConnectionString)
            member_Connection.Open()

        Catch ex As OleDbException

            member_Exception = ex.ToString

            Return False

        Finally

            CloseConnection()

        End Try

        member_Exception = "No Error"


        Return True

    End Function

    ''' <summary>
    ''' SQL Function for any SQL Command Syntax.
    ''' </summary>
    ''' <param name="SQLQuery">A Valid SQL Query Command.</param>
    ''' <param name="ConnType"><see>ConnectioType</see></param>
    ''' <returns>True=Command Success, False=Command Failed</returns>
    ''' <example>
    ''' 1. Obj.SQL("SELECT * FROM TABLE1",ConnectionType.DataReader)<br></br>
    '''    Returns OleDbDataReader<br></br>
    ''' 2. Obj.SQL("DELETE FROM TABLE1", ConnectionType.DataCommand)<br></br>
    ''' 3. Obj.SQL("INSERT INTO TABLE1 VALUES('VALUE')", ConnectionType.DataCommand)<br></br>
    ''' 4. Obj.SQL("UPDATE TABLE1 SET Field1='VALUE'", ConnectionType.DataCommand)<br></br>
    ''' 5. Obj.SQL("SELECT * FROM TABLE1", ConnectionType.DataAdapter)<br></br>
    '''    Returns DataTable
    ''' </example>
    Public Function SQL(ByVal SQLQuery As String, ByVal ConnType As ConnectionType) As Boolean

        If Connect(member_ConnectionString) = False Then Return False

        Try
            member_Connection.Open()

            Select Case ConnType

                Case ConnectionType.DataReader

                    member_Command = New OleDbCommand

                    member_Command.CommandText = SQLQuery
                    member_Command.Connection = member_Connection

                    member_DataReader = member_Command.ExecuteReader

                Case ConnectionType.DataCommand

                    member_Command = New OleDbCommand

                    member_Command.CommandText = SQLQuery
                    member_Command.Connection = member_Connection

                    member_Command.ExecuteNonQuery()
                    member_Command.Dispose()

                    CloseConnection()

                Case ConnectionType.DataAdapter

                    member_DataAdapter = New OleDbDataAdapter
                    member_Command = New OleDbCommand

                    member_Command.CommandText = SQLQuery
                    member_Command.Connection = member_Connection

                    member_DataAdapter.SelectCommand = member_Command

                    member_DataTable = New DataTable

                    With member_DataAdapter.SelectCommand.CommandType = CommandType.Text
                        member_DataAdapter.Fill(member_DataTable)
                    End With

                    member_DataAdapter.Dispose()

                    CloseConnection()

            End Select

        Catch ex As OleDbException

            member_Exception = ex.ToString

            Return False

        Catch ex As Exception

            member_Exception = ex.ToString

            Return False

        End Try

        member_Exception = "No Error"

        Return True

    End Function

#End Region

#Region "Data Procedures"

    ''' <summary>
    ''' Procedure for populating data into a ListView Object with the current DataReader.
    ''' </summary>
    ''' <param name="lstView">A Valid Listview Object</param>
    ''' <param name="SQLQuery">A Valid SQL SELECT Query Command</param>
    ''' <param name="CreateColumns">Directs the listview object to create new Columns that is equal to the SQL Fields.</param>
    ''' <param name="AutoResize">For the listview to autoresize width.</param>
    ''' <param name="AlternateColors">Directs the listview to change colors alternately</param>
    ''' <param name="AlternateListViewColor">The Color of the listview</param>
    ''' <returns>True=Success, False=Failed</returns>
    ''' <remarks>Accepts SQL SELECT Query only</remarks>
    ''' <example>Obj.fillListview(ListView,"SELECT * FROM TABLE1")</example>
    Public Function FillListview(ByRef lstView As ListView, _
                                 ByVal SQLQuery As String, _
                                 ByVal CreateColumns As Boolean, _
                                 ByVal AutoResize As Boolean, _
                                 Optional ByVal AlternateColors As Boolean = False, _
                                 Optional ByVal AlternateListViewColor As Object = Nothing) As Boolean

        If SQL(SQLQuery, ConnectionType.DataReader) = False Then Return False

        Dim lvwColumn As ColumnHeader = Nothing
        Dim itmListItem As ListViewItem = Nothing
        Dim cColor As Color = Color.White

        Dim firstRow As String
        Dim ctr As Short

        lstView.View = View.Details
        lstView.FullRowSelect = True

        If CreateColumns = True Then
            lstView.Clear()
        Else
            lstView.Items.Clear()
        End If

        Try

            If CreateColumns = True Then

                For ctr = 0 To member_DataReader.FieldCount() - 1

                    lvwColumn = New ColumnHeader()
                    lvwColumn.Text = member_DataReader.GetName(ctr)
                    lstView.Columns.Add(lvwColumn)

                Next

            End If


            While member_DataReader.Read

                itmListItem = New ListViewItem()
                firstRow = IIf(member_DataReader.IsDBNull(0), "", member_DataReader(0))
                itmListItem.Text = firstRow

                For ctr = 1 To member_DataReader.FieldCount() - 1

                    If member_DataReader.IsDBNull(ctr) Then
                        itmListItem.SubItems.Add("")
                    Else
                        itmListItem.SubItems.Add(member_DataReader(ctr))
                    End If

                Next ctr

                lstView.Items.Add(itmListItem)

            End While

            If AutoResize = True Then
                For ctr = 0 To lstView.Columns.Count - 1
                    lstView.Columns(ctr).AutoResize(ColumnHeaderAutoResizeStyle.HeaderSize)
                Next
            End If

            If AlternateColors Then

                Try
                    cColor = CType(AlternateListViewColor, Color)
                Catch ex As Exception
                    cColor = Color.White
                End Try

                ColorListview(lstView, cColor)

            End If

        Catch ex As Exception

            member_Exception = ex.ToString

            Return False

        Finally

            CloseConnection()

        End Try

        Return True

    End Function

    ''' <summary>
    ''' Procedure for populating data into a ComboBox Object with the current DataReader.
    ''' </summary>
    ''' <param name="cmbBox">A Valid ComboBox Object.</param>
    ''' <param name="SQLQuery">A Valid SQL SELECT Query Command.</param>
    ''' <returns>True=Success, False=Failed</returns>
    ''' <remarks>
    ''' Use only SELECT Command with a single field selection<br></br>
    ''' e.g<br></br>
    ''' SELECT Field1 FROM Table1
    ''' </remarks>
    ''' <example>Obj.fillCombobox(ComboBox,"SELECT Field1 FROM Table1")</example>
    Public Function FillCombobox(ByRef CmbBox As ComboBox, ByVal SQLQuery As String) As Boolean

        If SQL(SQLQuery, ConnectionType.DataReader) = False Then Return False

        CmbBox.Items.Clear()

        Try

            While member_DataReader.Read

                CmbBox.Items.Add(member_DataReader(0))

            End While

        Catch ex As Exception

            member_Exception = ex.ToString

            Return False

        Finally

            CloseConnection()

        End Try

        Return True

    End Function

    ''' <summary>
    ''' Procedure to populate data into a Listview Object using an existing DataTable Object.
    ''' </summary>
    ''' <param name="lstView">A Valid ListView Object.</param>
    ''' <param name="dTable">A Valid DataTable Object.</param>
    ''' <param name="Header">Copies DataTable Header into ListView Columns.</param>
    ''' <param name="AutoResize">Directs the listview to autoresize.</param>
    ''' <param name="AlternateColors">Directs the listview to change colors alternately</param>
    ''' <param name="AlternateListViewColor">The Color of the listview</param>
    ''' <returns>True=Success, False=Failed</returns>
    ''' <remarks>Accepts SQL SELECT Query only.</remarks>
    ''' <example>Obj.dataTableToListview(ListView,DataTable,True,True)</example>
    Public Function DataTableToListview(ByRef lstView As ListView, _
                                        ByRef dTable As DataTable, _
                                        ByVal Header As Boolean, _
                                        ByVal AutoResize As Boolean, _
                                        Optional ByVal AlternateColors As Boolean = False, _
                                        Optional ByVal AlternateListViewColor As Object = Nothing) As Boolean

        Dim lvwColumn As ColumnHeader = Nothing
        Dim itmListItem As ListViewItem = Nothing
        Dim cColor As Color = Color.White

        Try

            If Header = True Then

                lstView.Clear()

                For i As Integer = 0 To dTable.Columns.Count - 1

                    lvwColumn = New ColumnHeader()

                    lvwColumn.Text = dTable.Columns(i).ToString

                    lstView.Columns.Add(lvwColumn)

                Next

            Else

                lstView.Items.Clear()

            End If


            For i As Integer = 0 To dTable.Rows.Count - 1

                itmListItem = New ListViewItem
                itmListItem.Text = dTable.Rows(i)(0).ToString

                For index As Integer = 1 To dTable.Columns.Count - 1
                    itmListItem.SubItems.Add(dTable.Rows(i)(index).ToString)
                Next

                lstView.Items.Add(itmListItem)

            Next

            If AutoResize Then
                For ctr = 0 To lstView.Columns.Count - 1
                    lstView.Columns(ctr).AutoResize(ColumnHeaderAutoResizeStyle.HeaderSize)
                Next
            End If

            If AlternateColors Then

                Try
                    cColor = CType(AlternateListViewColor, Color)
                Catch ex As Exception
                    cColor = Color.White
                End Try

                ColorListview(lstView, cColor)

            End If

        Catch ex As Exception

            member_Exception = ex.ToString

            Return False

        End Try

        Return True

    End Function

    ''' <summary>
    ''' Procedure for populating Table Fields into a String Array.
    ''' </summary>
    ''' <param name="SQLQuery">A Valid SQL SELECT Query Command.</param>
    ''' <returns>An array of string consisting the current fields of a table.</returns>
    ''' <remarks>
    ''' 1. Accepts SQL SELECT Query only<br></br>
    ''' 2. SELECT Top 1 for optimized performance.
    ''' </remarks>
    ''' <example>Obj.GetFieldNames("SELECT * FROM Table1")</example>
    Public Function GetFieldNames(ByVal SQLQuery As String) As String()

        On Error Resume Next

        If SQL(SQLQuery, ConnectionType.DataReader) = False Then Return Nothing

        Dim fields(member_DataReader.FieldCount() - 1) As String

        For i As Integer = 0 To member_DataReader.FieldCount() - 1

            fields(i) = member_DataReader.GetName(i)

        Next

        Return fields

    End Function

    ''' <summary>
    ''' Alternatly draws color to a given ListView Object.
    ''' </summary>
    ''' <param name="lstView">A Valid ListView Object.</param>
    ''' <param name="AlternateColor">Color of the ListView</param>
    ''' <example>Obj.colorListview(ListView,System.Drawing.Color.Blue)</example>
    Public Sub ColorListview(ByRef lstView As ListView, ByVal AlternateColor As Drawing.Color)
        For i As Integer = 0 To lstView.Items.Count - 1
            If i Mod 2 = 0 Then
                lstView.Items(i).BackColor = AlternateColor
            Else
                lstView.Items(i).BackColor = Drawing.Color.White
            End If
        Next i
    End Sub

#End Region

#Region "Deconstructor"

    ''' <summary>
    ''' Deconstructor of the object.
    ''' </summary>
    ''' <example>Obj.Dispose()</example>
    Public Sub Dispose() Implements IDisposable.Dispose
        On Error Resume Next
        Dispose(True)           'Deconstructing Sub Routine
        GC.SuppressFinalize(Me) 'Garbage Collector
    End Sub

    ''' <summary>
    ''' Deconstructing routine of the object.
    ''' </summary>
    ''' <example>Dispose(True)</example>
    Protected Overridable Sub Dispose(ByVal disposing As Boolean)

        If Not isDisposed Then

            If disposing Then

                CloseConnection()

                member_ConnectionString = ""
                member_Exception = ""

                If Not member_DataTable Is Nothing Then
                    member_DataTable.Dispose()
                    member_DataTable = Nothing
                End If

                If Not member_DataAdapter Is Nothing Then
                    member_DataAdapter.Dispose()
                    member_DataAdapter = Nothing
                End If

                If Not member_Command Is Nothing Then
                    member_Command.Dispose()
                    member_Command = Nothing
                End If

                If Not member_Connection Is Nothing Then
                    member_Connection.Close()
                    member_Connection = Nothing
                End If

            End If
        End If

        isDisposed = True

    End Sub

#End Region

End Class
